	    LIST P=16F628AA;Se define el microcontrolador a programar
		INCLUDE <P16F628A.INC>;se incluye archivo de etiquetas o nombres
		CBLOCK	0X20
		R1,R2,R3,R4,INDICE,SALVA,TEMP,COL,TEC
		ENDC
#DEFINE	RS	PORTA,0
#DEFINE EN	PORTA,1	
		
	    ORG	0X2007
	    DW 0X3E10
		ORG 0
		MOVLW	0X07
		MOVWF	CMCON;Apagar el comparador analogo
		BANKSEL	TRISA
		BCF		TRISA,0;RA0 ES SALIDA(RS)
		BCF		TRISA,1;RA1 ES SALIDA(EN)
	    MOVLW	B'00001111'
	    MOVWF	TRISB
		BANKSEL PORTB
		CLRF	PORTB
		CLRF	PORTA
		MOVLW	.10
		CALL	MILIS ; Pausa  para que los niveles de tension sean estables
		
		;Enviando comandos para configurar el display LCD
		MOVLW	0X32
		CALL	ENV_CMD
		MOVLW	0X28
		CALL	ENV_CMD
		MOVLW	0X0C
		CALL	ENV_CMD
		MOVLW	0X06
		CALL	ENV_CMD
		MOVLW	0X01
		CALL	ENV_CMD
		;Enviando posicion de cursor
		MOVLW	0X84
		CALL	ENV_CMD; fila 1 columna 5
		
		;Enviando caracteres
		MOVLW	'P'
		CALL	ENV_CAR
		MOVLW	.200
		CALL	MILIS
		MOVLW	'E'
		CALL	ENV_CAR
		MOVLW	.200
		CALL	MILIS
		
		MOVLW	'R'
		CALL	ENV_CAR
		MOVLW	.200
		CALL	MILIS
		
		MOVLW	'U'
		CALL	ENV_CAR
Z2		
		MOVLW	0XC7
		CALL 	ENV_CMD
Z1		CALL	TECLADO
		MOVWF	SALVA
		SUBLW	0
		BZ		Z1
		MOVFW   SALVA
		CALL	AJUSTE_TECLA
		CALL	ENV_CAR
		GOTO	Z2
		GOTO	$ ;Anclar el programa
		
ENV_CMD
		BCF		RS	;RS=0-->SELECCION DEL REGISTRO DE COMANDOS
		CALL	ENV_BYTE
		RETURN
ENV_CAR
		BSF		RS	;RS=1-->SELECCION DE REGISTRO DE CARACTERES
		CALL	ENV_BYTE
		RETURN		
ENV_BYTE
		MOVWF	TEMP
		MOVWF	PORTB
		BSF		EN
		CALL	T1
		BCF		EN
		CALL	T1
		SWAPF   TEMP,W
		MOVWF	PORTB
		BSF		EN
		CALL	T1
		BCF		EN
		CALL	T1
		RETURN
TECLADO
		MOVLW	B'11110000'
		MOVWF   PORTB
		MOVFW	PORTB
		ANDLW	B'00001111'
		BTFSC	STATUS,Z
		RETLW	0
		MOVLW	B'00010000'
		MOVWF	COL
		MOVLW	.1
		MOVWF	TEC
S_COL		
		MOVFW	COL
		MOVWF	PORTB
		BTFSC	PORTB,0
		GOTO	FILA1
		BTFSC	PORTB,1
		GOTO	FILA2
		BTFSC   PORTB,2
		GOTO	FILA3
		BTFSC	PORTB,3
		GOTO	FILA4
		RLF		COL,F
		INCF 	TEC,F
		BTFSS	STATUS,C
		GOTO	S_COL
		RETLW	0				
FILA1
		MOVFW	TEC
		RETURN
FILA2
		MOVFW	TEC
		ADDLW	.4
		RETURN
FILA3
		MOVFW	TEC
		ADDLW	.8
		RETURN
FILA4		
		MOVFW	TEC
		ADDLW	.12
		RETURN
AJUSTE_TECLA
		ADDWF PCL,F
		DT " 789/456X123-C0=+"
		;DT " 123A456B789XC0ME"		
MILIS
		
		MOVWF R2
		CALL T1
		DECFSZ R2,F
		GOTO $-2
		RETURN		
T1
		MOVLW	.248
		MOVWF R1
		NOP
		DECFSZ R1,F
		GOTO $-2
		NOP
		NOP
		NOP
		RETURN
		END		